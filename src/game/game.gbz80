INCLUDE "hardware.inc"
INCLUDE "player.inc"
INCLUDE "spriteAnim.inc"
INCLUDE "enemy.inc"

DEF GROUND_HEIGHT = $0E

SECTION "MainGameRAM", WRAM0

screenScrollSpeed: ds 2
screenScrollPos: ds 2

SECTION "MainGame", ROM0
game::
    ld a, [firstStateFrame]
    or a, 0 ;if the flag is nonzero, we already initialized everything
    jr nz, .skipFirstFrameLoading

    call initFrame

.skipFirstFrameLoading:

	call updatePlayer
	;call handleEnemySpawn
	;call updateEnemies
	call updateScrolling

	ld a, [screenScrollPos]
	ld [backgroundX], a

    ret


;updates the scrolling using the scrollSpeed specified by our scrollSpeed variable
;i plan to make that speed increase as time goes on, to increase difficulty
;the screen scroll speed is also what will describe at which speed the enemies will move to the left
updateScrolling:
    ld a, [screenScrollPos+1]
	ld b, a
    ld a, [screenScrollSpeed+1]
	add a, b
	ld [screenScrollPos+1], a

    ld a, [screenScrollPos]
	ld b, a
    ld a, [screenScrollSpeed]
    adc a, b
	ld [screenScrollPos], a
	ret

;called once everytime the game state got changed to the main one,
;serves for initializing the main game state properly so that the game
;doesn't glitch out
initFrame:
    call LCDOff ;turn off screen for setup
    
    ld d, 0
    ld hl, _SCRN0
    ld bc, SCRN_VX_B * (SCRN_Y_B-4)
    call memset ;clear up the title mostly

    ld d, 1
    ld hl, _SCRN0 + (SCRN_VX_B * $0E)
    ld bc, SCRN_VX_B
    call memset ;add grass tiles

    ld d, 2
    ld hl, _SCRN0 + (SCRN_VX_B * $0F)
    ld e, 0
.groundLoop:
    ld bc, SCRN_VX_B
    call memset   ;repeat memsets to add the ground tiles under grass
    ld a, d
    ld d, 0
    add hl, de
    ld d, a
    ld a, $40
    cp l
    jr nz, .groundLoop

    ld a, LCDCF_ON | LCDCF_BGON | LCDCF_OBJON
    ld [rLCDC], a ;turn back on the LCD

	;start position of the player, should define them
    ld a, $20
    ld [playerPosX], a 
    ld a, $78
    ld [playerPosY], a

	;initializing to 0 most variables
    ld a, $00
    ld [playerPosX+1], a
    ld [playerPosY+1], a
    ld [playerVelX], a
    ld [playerVelX+1], a
    ld [playerVelY], a
    ld [playerVelY+1], a
    ld [playerAccelX], a
    ld [playerAccelX+1], a
    ld [playerAccelY], a
    ld [playerAccelY+1], a
	ld [screenScrollSpeed], a
	ld [screenScrollPos], a
	ld [screenScrollPos+1], a
	ld [EnemySpawnTimer], a

	ld d, 0
	ld hl, EnemySlots
	ld bc, Enemy_Size * 4 ;we wanna clear all enemy slots
	call memset

	;setting the flag that indicates memory has been initialized
	;also sets up the playerTile, not sure it's still needed, but we never know
    ld a, 1
    ld [firstStateFrame], a
    ld [playerTile], a

	;base scroll speed for now, since scroll speed increase is not yet implemented
	ld a, $80
	ld [screenScrollSpeed+1], a

    ret
